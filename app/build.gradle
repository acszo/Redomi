plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))

def versionMajor = 1
def versionMinor = 2
def versionPatch = 3

def appVersionCode = "${versionMajor}${formatVersion(versionMinor)}${formatVersion(versionPatch)}" as Integer
def appVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

static def formatVersion(version) {
    return version < 10 ? "0${version}" : version
}

android {
    androidResources {
        generateLocaleConfig true
    }
    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    namespace 'com.acszo.redomi'
    compileSdk 35

    defaultConfig {
        applicationId 'com.acszo.redomi'
        minSdk 28
        targetSdk 35
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary true
        }
        signingConfig signingConfigs.release
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    packagingOptions {
        resources {
            excludes += '**'
        }
    }
}

dependencies {

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2024.09.03')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // Compose
    implementation platform('androidx.compose:compose-bom:2024.09.03')
    implementation 'androidx.compose.material3:material3:1.3.0'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.animation:animation-graphics'
    implementation 'androidx.activity:activity-compose:1.9.3'

    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.1.0-rc01'

    // Dagger Hilt
    implementation 'com.google.dagger:hilt-android:2.52'
    kapt 'com.google.dagger:hilt-compiler:2.52'
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-kotlinx-serialization:2.11.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.2'

    // ViewModel Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'

    // Coil
    implementation 'io.coil-kt:coil-compose:2.7.0'

    // Data Store
    implementation 'androidx.datastore:datastore-preferences:1.1.1'

    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.7.7'

    implementation project(':squircle')

}